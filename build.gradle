/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id "com.github.sherter.google-java-format" version "0.7.1"
  id "net.ltgt.errorprone" version "0.0.16" apply false
}

repositories {
  // to get the google-java-format jar and dependencies
  jcenter()
}

subprojects { project ->
    afterEvaluate {
        if (project.plugins.hasPlugin("java")
            || project.plugins.hasPlugin("com.android.application")) {
            project.apply plugin: "net.ltgt.errorprone"
            project.dependencies {
                errorprone deps.build.errorProneCore
            }
            project.tasks.withType(JavaCompile) {
                options.compilerArgs += [
                    "-Xlint:unchecked",
                    "-Xlint:rawtypes",
                    "-Werror",
                    // don't analyze generated code; AutoValue code fails UnnecessaryParentheses check
                    "-XepExcludedPaths:.*/build/classes/.*",
                    // this check is too noisy
                    "-Xep:StringSplitter:OFF",
                    "-Xep:WildcardImport:ERROR"
                ]
            }
        }
    }
    buildscript {
        repositories {
            jcenter()
            maven {
               url "https://plugins.gradle.org/m2/"
            }
        }
    }

    repositories {
        maven {
            url "https://raw.githubusercontent.com/lazaroclapp/WALA/repository/"
        }
        jcenter()
        google()
    }

}

googleJavaFormat {
  toolVersion = "1.6"
}

apply from: "gradle/dependencies.gradle"
