/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
    repositories {
        mavenCentral()
        google() // For Gradle 4.0+
    }

    dependencies {
        classpath libs.android.gradle.plugin
        classpath libs.gradle.maven.publish.plugin
    }
}

plugins {
    alias(libs.plugins.spotless)
    alias(libs.plugins.errorprone) apply false
    alias(libs.plugins.shadow) apply false
    alias(libs.plugins.jmh) apply false
    alias(libs.plugins.versions)
    alias(libs.plugins.shellcheck)
}

ext {
    errorProneVersionToCompileAgainst = libs.versions.errorProne.get()
    latestErrorProneVersion = libs.versions.errorProne.get()
}



repositories {
    // to get the google-java-format jar and dependencies
    mavenCentral()
}

def currentJavaVersion = JavaVersion.current()
if (!currentJavaVersion.isCompatibleWith(JavaVersion.VERSION_21)) {
    throw new GradleException(
            "NullAway only builds on JDK 21 or higher now. Detected ${System.getProperty('java.version')}. Please upgrade to JDK 21 or later."
    )
}

if (currentJavaVersion == JavaVersion.VERSION_21) {
    def runtimeVersion = Runtime.version()
    if (runtimeVersion.feature() == 21 && runtimeVersion.update() < 8) {
        throw new GradleException(
                "Building NullAway on JDK 21 requires at least 21.0.8, but found ${runtimeVersion}.  Please upgrade to 21.0.8 or later, or to the most recent JDK release."
        )
    }
}

subprojects { project ->
    project.apply plugin: "net.ltgt.errorprone"
    project.dependencies {
        errorprone libs.error.prone.core
    }
    project.tasks.withType(JavaCompile) {
        dependsOn(installGitHooks)
        // Always compile using the latest JDK we can
        javaCompiler = javaToolchains.compilerFor {
            languageVersion = JavaLanguageVersion.of(25)
        }
        options.compilerArgs += [
                "-Xlint:deprecation",
                "-Xlint:rawtypes",
                "-Xlint:unchecked",
                "-Werror",
                "-parameters",
        ]
        options.errorprone {
            // disable warnings in generated code; AutoValue code fails UnnecessaryParentheses check
            disableWarningsInGeneratedCode = true
            // this check is too noisy
            check("StringSplitter", CheckSeverity.OFF)
            // https://github.com/google/error-prone/issues/3366
            check("CanIgnoreReturnValueSuggester", CheckSeverity.OFF)
            // turn up various checks
            check("WildcardImport", CheckSeverity.ERROR)
            check("MissingBraces", CheckSeverity.ERROR)
            check("TypeToString", CheckSeverity.ERROR)
            check("SymbolToString", CheckSeverity.ERROR)
            check("MultipleTopLevelClasses", CheckSeverity.ERROR)
            check("ClassName", CheckSeverity.ERROR)
            check("PackageLocation", CheckSeverity.ERROR)
            check("UnnecessaryAnonymousClass", CheckSeverity.ERROR)
            check("UnusedException", CheckSeverity.ERROR)
            check("UnnecessaryFinal", CheckSeverity.ERROR)
            check("PreferredInterfaceType", CheckSeverity.ERROR)
            check("AnnotationPosition", CheckSeverity.ERROR)
            check("VoidMissingNullable", CheckSeverity.ERROR)
            check("EqualsMissingNullable", CheckSeverity.ERROR)
            // To enable auto-patching, uncomment the line below, replace [CheckerName] with
            // the checker(s) you want to apply patches for (comma-separated), and above, disable
            // "-Werror"
            // errorproneArgs.addAll("-XepPatchChecks:[CheckerName]", "-XepPatchLocation:IN_PLACE")
        }
    }

    // We need to use the older sourceCompatibility / targetCompatibility settings, rather than the newer "release"
    // option, since we use internal javac APIs, which "release" doesn't allow
    tasks.withType(JavaCompile) {
        java.sourceCompatibility = JavaVersion.VERSION_11
        java.targetCompatibility = JavaVersion.VERSION_11
    }

    tasks.withType(Test).configureEach {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }

    repositories {
        mavenCentral()
        google()
    }

    // Spotless complains when applied to the folders containing projects
    // when they do not have a build.gradle file
    if (project.name != "jar-infer" && project.name != "library-model" && project.name != "jdk-annotations" && project.name != "nullaway-bom") {
        project.apply plugin: "com.diffplug.spotless"
        spotless {
            java {
                googleJavaFormat(libs.versions.google.java.format.get())
            }
        }
    }
}

spotless {
    predeclareDeps()
    groovyGradle {
        target '**/*.gradle'
        greclipse()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}
spotlessPredeclare {
    java { googleJavaFormat(libs.versions.google.java.format.get()) }
    groovyGradle {
        greclipse()
    }
}

shellcheck {
    useDocker = false
    shellcheckBinary = "shellcheck"
    sourceFiles =
            fileTree(".") {
                include("**/*.sh")
            }
}

////////////////////////////////////////////////////////////////////////
//
//  Google Java Format pre-commit hook installation
//

tasks.register('installGitHooks', Copy) {
    from(file('config/hooks/pre-commit-stub')) {
        rename 'pre-commit-stub', 'pre-commit'
    }
    into file('.git/hooks')
    filePermissions {
        unix(0777)
    }
}
