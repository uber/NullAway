plugins {
    id "java"
    id "com.vanniktech.maven.publish"
    id "com.github.johnrengelman.shadow"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
}

dependencies {
    compile deps.build.commonscli
    compile deps.build.guava
    compileOnly deps.build.errorProneCheckApi

    compile project(":jar-infer:jar-infer-lib")

    testImplementation deps.test.junit4
    testImplementation(deps.build.errorProneTestHelpers) {
        exclude group: "junit", module: "junit"
    }
}

jar {
    manifest {
        attributes(
        'Main-Class': 'com.uber.nullaway.jarinfer.JarInfer'
        )
    }
}

shadowJar {
    mergeServiceFiles()
    configurations = [project.configurations.compile]
    classifier = null
}
shadowJar.dependsOn jar
assemble.dependsOn shadowJar

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        publication == publishing.publications.shadow
    }
}
tasks.withType(PublishToMavenLocal) {
    onlyIf {
        publication == publishing.publications.shadow
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            afterEvaluate {
                artifact project.sourcesJar {
                    classifier "sources"
                }
                artifact project.javadocsJar {
                    classifier "javadoc"
                }
            }
            pom {
                name = 'JarInfer'
                description = 'A JVM bytecode null auto-annotation tool for use with NullAway'
                url = project.property('POM_URL')
                licenses {
                    license {
                        name = project.property('POM_LICENCE_NAME')
                        url = project.property('POM_LICENCE_URL')
                        distribution = project.property('POM_LICENCE_DIST')
                    }
                }
                developers {
                    developer {
                        id = project.property('POM_DEVELOPER_ID')
                        name = project.property('POM_DEVELOPER_NAME')
                        url = project.property('POM_DEVELOPER_URL')
                    }
                }
                scm {
                    connection = project.property('POM_SCM_CONNECTION')
                    developerConnection = project.property('POM_SCM_DEV_CONNECTION')
                    url = project.property('POM_SCM_URL')
                }
            }
        }
    }
}
