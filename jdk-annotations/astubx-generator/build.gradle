plugins {
    id "java-library"
    id 'nullaway.java-test-conventions'
}

// Require JDK 21, since the jdk-javac-plugin requires it
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Allow Java 21 features for this module
tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

// Don't test on JDK 17; we require JDK 21
tasks.named("testJdk17").configure {
    onlyIf { false }
}

tasks.withType(Test).configureEach { test ->
    // Ensure the plugin JAR exists
    dependsOn(":jdk-javac-plugin:shadowJar")
}

dependencies {
    implementation project(":library-model:library-model-generator")
    implementation project(":jdk-javac-plugin")
    implementation libs.gson
    implementation libs.guava

    testImplementation libs.junit4
    testImplementation libs.jspecify
    testImplementation(libs.error.prone.test.helpers) {
        exclude group: "junit", module: "junit"
    }
}
