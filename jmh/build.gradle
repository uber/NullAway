plugins {
    id 'java-library'
    id "me.champeau.jmh" version "0.6.5"
}

configurations {
    // create a configuration for the sources and dependencies of each benchmark
    caffeineSources
    caffeineDeps
}
dependencies {

    // Add NullAway and Error Prone Core as dependencies.  This ensures that the classes get included
    // in the jmh-generated jar, and hence get JIT-compiled during benchmarking.  Without this dependence, NullAway
    // can still be loaded via the processor path, but it gets reloaded on each run of compilation, skewing
    // performance measurements
    implementation project(':nullaway')
    // use the same version of Error Prone Core that we are compiling NullAway against, so we can
    // benchmark against different versions of Error Prone
    implementation deps.build.errorProneCoreForApi


    // Source jars for our desired benchmarks
    caffeineSources('com.github.ben-manes.caffeine:caffeine:3.0.2:sources') {
        transitive = false
    }

    caffeineDeps 'com.github.ben-manes.caffeine:caffeine:3.0.2'

    testImplementation deps.test.junit4
}

def caffeineSourceDir = project.layout.buildDirectory.dir('caffeineSources')

task extractCaffeineSources(type: Copy) {
    from zipTree(configurations.caffeineSources.singleFile)
    into caffeineSourceDir
}

compileJava.dependsOn(extractCaffeineSources)

// always run jmh
tasks.getByName('jmh').outputs.upToDateWhen { false }

jmh {
    // seems we need more iterations to fully warm up the JIT
    warmupIterations = 10

    // a trick: to get the classpath for a benchmark, create a configuration that depends on the benchmark, and
    // then filter out the benchmark itself
    def caffeineClasspath = configurations.caffeineDeps.filter({f -> !f.toString().contains("caffeine-3.0.2")}).asPath
    // pass source directories and classpaths for benchmarks as JVM arguments
    // though jvmArgsAppend is a list, it just gets converted to a single string (probably a plugin bug)
    jvmArgsAppend = [
            "-Dnullaway.caffeine.sources=${caffeineSourceDir.get()} " +
            "-Dnullaway.caffeine.classpath=$caffeineClasspath"
    ]

    // commented-out examples of how to tweak other jmh parameters; they show the default values
    // for more examples see https://github.com/melix/jmh-gradle-plugin/blob/master/README.adoc#configuration-options
    // iterations = 5
    // fork = 5
}
