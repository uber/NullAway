/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.gradle.api.BaseVariant
import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
    id('com.android.application')
}

android {
    compileSdkVersion deps.build.compileSdkVersion

    defaultConfig {
        applicationId "com.uber.myapplication"
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    lintOptions {
        abortOnError false
    }

    DomainObjectSet<BaseVariant> variants = getApplicationVariants() // or getLibraryVariants() in libraries
    variants.addAll(getTestVariants())
    variants.addAll(getUnitTestVariants())
    variants.configureEach { variant ->
        variant.getJavaCompileProvider().configure {
            options.errorprone {
                check("NullAway", CheckSeverity.ERROR)
                option("NullAway:AnnotatedPackages", "com.uber")
            }
        }
    }

    // If you want to disable NullAway in just tests, you can do the below
    //    DomainObjectSet<BaseVariant> testVariants = getTestVariants()
    //    testVariants.addAll(getUnitTestVariants())
    //    testVariants.configureEach { variant ->
    //        variant.getJavaCompileProvider().configure {
    //            options.errorprone {
    //                check("NullAway", CheckSeverity.OFF)
    //            }
    //        }
    //    }
}

dependencies {
    implementation deps.support.appcompat
    annotationProcessor project(":nullaway")
    annotationProcessor project(path: ":sample-library-model")

    testImplementation deps.test.junit4
}

spotless {
    java {
        target 'src/*/java/**/*.java'
    }
}
